>> Sales All Region measure definition
==================================================
Sales All Region =
CALCULATE(SUM(Sales[Sales]), REMOVEFILTERS(Region))


>> Sales % All Region measure definition (replace Sales All Region measure)
==================================================
Sales % All Region =
DIVIDE(
	SUM(Sales[Sales]),
	CALCULATE(
		SUM(Sales[Sales]),
		REMOVEFILTERS(Region)
	)
)


>> Sales % Country measure definition
==================================================
Sales % Country =
DIVIDE(
	SUM(Sales[Sales]),
	CALCULATE(
		SUM(Sales[Sales]),
		REMOVEFILTERS(Region[Region])
	)
)


>> Sales % Country measure definition (2)
==================================================
Sales % Country =
IF(
	ISINSCOPE(Region[Region]),
	DIVIDE(
		SUM(Sales[Sales]),
		CALCULATE(
			SUM(Sales[Sales]),
			REMOVEFILTERS(Region[Region])
		)
	)
)


>> Sales % Group measure definition
==================================================
Sales % Group =
DIVIDE(
	SUM(Sales[Sales]),
	CALCULATE(
		SUM(Sales[Sales]),
		REMOVEFILTERS(
			Region[Region],
			Region[Country]
		)
	)
)


>> Sales % Group measure definition (2)
==================================================
Sales % Group =
IF(
	ISINSCOPE(Region[Region])
		|| ISINSCOPE(Region[Country]),
	DIVIDE(
		SUM(Sales[Sales]),
		CALCULATE(
			SUM(Sales[Sales]),
			REMOVEFILTERS(
				Region[Region],
				Region[Country]
			)
		)
	)
)


>> Sales table: Sales YTD measure definition
==================================================
Sales YTD =
TOTALYTD(SUM(Sales[Sales]), 'Date'[Date], "6-30")


>> Sales table: Sales YoY Growth
==================================================
Sales YoY Growth =
VAR SalesPriorYear =
	CALCULATE(
		SUM(Sales[Sales]),
		PARALLELPERIOD(
			'Date'[Date],
			-12,
			MONTH
		)
	)
RETURN
	SalesPriorYear


>> Sales table: Sales YoY Growth (2)
==================================================
Sales YoY Growth =
VAR SalesPriorYear =
	CALCULATE(
		SUM(Sales[Sales]),
		PARALLELPERIOD(
			'Date'[Date],
			-12,
			MONTH
		)
	)
RETURN
	DIVIDE(
		(SUM(Sales[Sales]) - SalesPriorYear),
		SalesPriorYear
	)
